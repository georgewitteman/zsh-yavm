#!/usr/bin/env zsh

typeset -gA _yavm_versions=()
local file_name variable_name command_name separator split_line command

# _yavm_debug "Searching for versions starting from '${PWD}'."

# First priority is environment variables. Something set as an environment
# variable is higher priority than a file.
for variable_name command_name in ${_yavm_version_variables}; do
  if [[ -z "${_yavm_versions[$command_name]}" && -n "${(P)variable_name}" ]]; then
    _yavm_versions[$command_name]="${(P)variable_name}"
  fi
done

local file_directory="$PWD"
while true; do
  for file_name command_name separator in ${_yavm_files_to_search}; do
    if [[ -f "$file_directory/$file_name" ]]; then

      if [[ "$command_name" = "ANY" ]]; then
        for line in "${(f)$(<$file_directory/$file_name)}"; do
          command="${line[(w)1]}"
          if [[ -z "${_yavm_versions[$command]}" ]]; then
            _yavm_versions[$command]="${line[(w)2,-1]}"
          fi
        done

      elif [[ -z "${_yavm_versions[$command_name]}" ]]; then
        if [[ "$separator" = "newline" ]]; then
          _yavm_versions[$command_name]="${(j: :)${(f)$(<$file_directory/$file_name)}}"
        else
          _yavm_versions[$command_name]="$(<$file_directory/$file_name)"
        fi
      fi

    fi
  done

  if [[ "$file_directory" = "/" || "$file_directory" = "$HOME" ]]; then
    break
  fi

  file_directory="${file_directory:a:h}"
done


# Finally, we set global versions if it wasn't set elsewhere. Only set global
# versions if they are actually set in a global file somewhere. We also don't
# need to set global versions explicitly if the file uses the normal search
# name and would have been already found. For example, the asdf global version
# file is named the same and is in the user's home directory.
for file_name command_name separator in ${_yavm_default_files}; do
  if [[ -z "${_yavm_versions[$command_name]}" && -f "${file_name}" ]]; then
    if [[ "${separator}" = "newline" ]]; then
      _yavm_versions[$command_name]="${(j: :)${(f)$(<$file_name)}}"
    else
      _yavm_versions[$command_name]="$(<$file_name)"
    fi
  fi
done
