#!/usr/bin/env zsh

typeset -gA _yavm_versions=()
local file_name variable_name command_name separator split_line command search_dir="$PWD"

# First priority is environment variables. Something set as an environment
# variable is higher priority than a file.
for variable_name command_name in ${(kv)yavm_version_variables}; do
  # echo "variable_name: $variable_name. command_name: $command_name"
  if [[ -z "${_yavm_versions[$command_name]}" && -n "${(P)variable_name}" ]]; then
    _yavm_versions[$command_name]="${(P)variable_name}"
    # echo "version found in var: $command_name -> ${(P)variable_name}"
  fi
done

# Next, lets look for version files up the directory tree until we get to $HOME
# or root. We check for both so we don't get into an infinite loop if we're not
# in a subdirectory of $HOME.
while true; do
  for file_name command_name in ${(kv)yavm_version_files}; do
    # echo "file_name: $file_name. command_name: $command_name"
    if [[ -f "$search_dir/$file_name" ]]; then

      if [[ "$command_name" = "ANY" ]]; then
        for line in "${(f)$(<$search_dir/$file_name)}"; do
          command="${line[(w)1]}"
          if [[ -z "${_yavm_versions[$command]}" ]]; then
            # echo "version found in file: $command -> ${line[(w)2,-1]}"
            _yavm_versions[$command]="${line[(w)2,-1]}"
          fi
        done

      elif [[ -z "${_yavm_versions[$command_name]}" ]]; then
        _yavm_versions[$command_name]="${(j: :)${(f)$(<$search_dir/$file_name)}}"
        # echo "version found in file: $command_name -> ${_yavm_versions[$command_name]}"
      fi

    fi
  done

  if [[ "$search_dir" = "/" || "$search_dir" = "$HOME" ]]; then
    break
  fi

  search_dir="${search_dir:a:h}"
done


# Finally, we set global versions if it wasn't set elsewhere. Only set global
# versions if they are actually set in a global file somewhere. We also don't
# need to set global versions explicitly if the file uses the normal search
# name and would have been already found. For example, the asdf global version
# file is named the same and is in the user's home directory.
for file_name command_name in ${(kv)yavm_default_version_files}; do
  if [[ -z "${_yavm_versions[$command_name]}" && -f "${file_name}" ]]; then
    _yavm_versions[$command_name]="${(j: :)${(f)$(<$file_name)}}"
    # echo "version found in default file: $command_name -> ${_yavm_versions[$command_name]}"
  fi
done

# echo "$PWD"
# echo "${(kv)_yavm_versions}"
