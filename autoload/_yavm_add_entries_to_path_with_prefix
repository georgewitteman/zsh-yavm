#!/usr/bin/env zsh

local prefix="$1"
shift

local entries=("$@")
local current_versions_in_path=(${(M)path:#$prefix/*})
local entries_to_add=("${@:|current_versions_in_path}")
local entries_to_remove=("${current_versions_in_path[@]:|entries}")

if [[ "${#current_versions_in_path}" -eq 0 ]]; then
  local index_to_add_at=1
else
  local index_to_add_at="${path[(ie)${current_versions_in_path[1]}]}"
fi

# Keep this after finding the idx because we want to keep the existing index
# even if we remove all the existing entries.
path=("${path[@]:|entries_to_remove}")

_yavm_debug "entries to remove: $entries_to_remove"
_yavm_debug "entries to add: $entries_to_add"

# Add new entries at the same position
local entry
for entry in ${(Oa)entries_to_add}; do
  path=(${path[1, $index_to_add_at - 1]} "$entry" ${path[$index_to_add_at, -1]})
done
