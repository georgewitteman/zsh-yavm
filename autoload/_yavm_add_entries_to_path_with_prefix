#!/usr/bin/env zsh

local prefix="$1" force="$2" path_tmp index_to_add_at
shift 2

if [[ -n "$force" ]]; then
  index_to_add_at=1
else
  index_to_add_at="${path[(i)$prefix*]}"
fi

if [[ "$index_to_add_at" -gt "${#path}" ]]; then
  index_to_add_at=1
fi

# Remove all of the current versions to make sure that everything ends up in
# the path in the correct order. Also, assign to a different variable because
# $path checks for duplicates. Assigning to a different variable skips that.
path_tmp=(${path:#${prefix}*})

# Add new entries at the same position. Note that we reverse the '$entries'
# array to make sure everything is added in the correct order.
path=(${path_tmp[1, $index_to_add_at - 1]} ${@:a} ${path_tmp[$index_to_add_at, -1]})
